dtmc

const double p = 0.5;
const int x1=1;
const int x2=2;
const int x3;
const int x4;
const int x5;
const int x6;

module die

        // local state
        s : [0..7] init 0;
        // value of the dice
        d : [0..6] init 0;
        AUX: [0..2] init 0;

        [] s=0 & AUX=0 -> p : (AUX'=1) + 1-p : (AUX'=2);
        [] s=0 & AUX=1 -> 1 : (s'=x1) & (AUX'=0);
        [] s=0 & AUX=2 -> 1 : (s'=x2) & (AUX'=0);
        //[] s=1 -> p : (s'=x3) + 1-p : (s'=x4);
        [] s=1 & AUX=0 -> p : (AUX'=1) + 1-p : (AUX'=2);
        [] s=1 & AUX=1 -> 1 : (s'=x3) & (AUX'=0);
        [] s=1 & AUX=2 -> 1 : (s'=x4) & (AUX'=0);
        [] s=2 & AUX=0 -> p : (AUX'=1) + 1-p : (AUX'=2);
        [] s=2 & AUX=1 -> 1 : (s'=x5) & (AUX'=0);
        [] s=2 & AUX=2 -> 1 : (s'=x6) & (AUX'=0);
        [] s=3 -> p : (s'=1) + 1-p : (s'=7) & (d'=1);
        [] s=4 -> p : (s'=7) & (d'=3) + 1-p : (s'=7) & (d'=2);
        [] s=5 -> p : (s'=2) + 1-p : (s'=7) & (d'=4);
        [] s=6 -> p : (s'=7) & (d'=6) + 1-p : (s'=7) & (d'=5);
        [] s=7 -> 1: (s'=7);

endmodule

rewards "coin_flips"
        [] s<7 : 1;
endrewards

label "one" = s=7&d=1;
label "two" = s=7&d=2;
label "three" = s=7&d=3;
label "four" = s=7&d=4;
label "five" = s=7&d=5;
label "six" = s=7&d=6;
label "end" = s=7;
